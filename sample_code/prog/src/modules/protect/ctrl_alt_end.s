;************************************************************************
;	[CTRL+ALT+END]キーの同時押下検出処理
;========================================================================
;■書式		: DWORD ctrl_alt_end(key);
;
;■引数
;	key		: 入力されたキーコード
;
;■戻り値	: [CTRL+ALT+END]キーの同時押下が検出された時、0以外の値
;************************************************************************
ctrl_alt_end:
		;---------------------------------------
		; 【スタックフレームの構築】
		;---------------------------------------
												; ------|--------
												; EBP+ 8| キーコード
												; ------+----------------
		push	ebp								; EBP+ 4| EIP（戻り番地）
		mov		ebp, esp						; EBP+ 0| EBP（元の値）
												; ------+----------------

		;---------------------------------------
		; キー状態保存
		;---------------------------------------
		mov		eax, [ebp + 8]					; EAX = key;
		btr		eax, 7							; CF  = EAX & 0x80;
		jc		.10F							; if (0 == CF)
		bts		[.key_state], eax				; {
		jmp		.10E							;   // フラグセット
.10F:											; } else {
		btc		[.key_state], eax				;   // フラグクリア
.10E:											; }

		;---------------------------------------
		; キー押下判定
		;---------------------------------------
												; do
												; {
		mov		eax, 0x1D						;   // [Ctrl]キーが押されているか？
		bt		[.key_state], eax				;   if (0 == key)
		jnc		.20E							;     break;
												;     
		mov		eax, 0x38						;   // [Alt]キーが押されているか？
		bt		[.key_state], eax				;   if ('ALT' != key)
		jnc		.20E							;     break;
												;     
		mov		eax, 0x4F						;   // [End]キーが押されているか？
		bt		[.key_state], eax				;   if ('End' != key)
		jnc		.20E							;     break;
												;   
		mov		eax, -1							;   ret = -1;
												;   
.20E:											; } while (0);

		sar		eax, 8							; ret >>= 8;

		;---------------------------------------
		; 【スタックフレームの破棄】
		;---------------------------------------
		mov		esp, ebp
		pop		ebp

		ret

.key_state:	times 32 db 0

